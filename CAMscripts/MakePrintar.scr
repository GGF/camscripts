'11.04.2014
'Добавлено сжатие таблицы апертур для ДПП
'Отредактирован сдвиг на 61 мм вверх для заготовок ДПП шириной по Y от 180 до 250 мм
'22.04.2014
'Установлен минимальный размер ДПП 300 мм
'25.04.2014 и 28.04.2014
'Установлены параметры экспорта gbr файлов: позитив, без зеркала
'2.03.2015
'Удаление слишком широкой рамки ДПП (т. к. иначе не находит прицелы)
'13.01.2016
'плевательницы на технологических полях
'15.02.2016
'исправлена ошибка - координаты прицелов после поворота

'---- constants ------
Block=-1
Small=0
Middle=1
Pferd=2
X=0
Y=1
Bot=0
Top=1
Left=0
Right=1


DIM Sizes#(Pferd,1)
Sizes#(Small,X)=230.0
Sizes#(Small,Y)=395.0
Sizes#(Middle,X)=280.0
Sizes#(Middle,Y)=395.0
Sizes#(Pferd,X)=485.0
Sizes#(Pferd,Y)=585.0

DIM Targets#(Pferd,Y,Top)
Targets#(Small,X,Left)=32.5
Targets#(Small,Y,Bot)=25.0
Targets#(Small,X,Right)=197.5
Targets#(Small,Y,Top)=370.0
Targets#(Middle,X,Left)=32.5
Targets#(Middle,Y,Bot)=25.0
Targets#(Middle,X,Right)=247.5
Targets#(Middle,Y,Top)=370.0
Targets#(Pferd,X,Left)=32.5
Targets#(Pferd,Y,Bot)=25.0
Targets#(Pferd,X,Right)=452.5
Targets#(Pferd,Y,Top)=560.0

DIM RotatedTargets#(Pferd,Y,Top)
RotatedTargets#(Small,X,Left)=137.5
RotatedTargets#(Small,Y,Bot)=93.5
RotatedTargets#(Small,X,Right)=482.5
RotatedTargets#(Small,Y,Top)=258.5
RotatedTargets#(Middle,X,Left)=137.5
RotatedTargets#(Middle,Y,Bot)=32.5
RotatedTargets#(Middle,X,Right)=482.5
RotatedTargets#(Middle,Y,Top)=247.5
RotatedTargets#(Pferd,X,Left)=42.5
RotatedTargets#(Pferd,Y,Bot)=32.5
RotatedTargets#(Pferd,X,Right)=577.5
RotatedTargets#(Pferd,Y,Top)=452.5

OneSide = -1
ClearMrk = 0

TargLeftX# = 0.0
TargBotY# = 0.0

TargRightX# = 0.0
TargTopY# = 0.0

DPPerror=0
'------------------------- output paths and filenames
!INCLUDE "getpathandname.scr"
'break
if mainboard$ = "" then
	MessageBox  "Неверный путь к плате"+ dbname!+CR!+"Скрипт следует запускать из подкаталога Printar", "Ошибка",ERROR 
	goto endofscript
end if

translit$ = customer$
gosub translit
customer$ = translit1$
translit$ = mainboard$
gosub translit
mainboard$ = translit1$
OutCustom$="M:\Printar\"+customer$
CustExist=FILEEXISTS(OutCustom$)
Outpath$=OutCustom$ + "\"+mainboard$
BrdExist=FILEEXISTS(Outpath$)

'break

Thickness#=1.5

!include "layersset.scr"

setresolution@ 100
setsnap@ 0
setgridsnap@ 0


if rcn=-1 and rsn = -1 then 
	print "Маркировка отсутствует"
	goto endofscript
end if


OpenForm #1,60,8,"Получение файлов для Printar", character, okcancel
if CustExist=0 then
	AddForm #1,0,1, "Будет создан каталог "+OutCustom$,PRINT
end if
if BrdExist then
	AddForm #1,0,2,"Каталог "+ Outpath$ + " существует. Файлы в нем будут переписаны." ,PRINT
else
	AddForm #1,0,2,"Файлы будут записаны в каталог "+ Outpath$,PRINT
end if
AddForm #1, 10, 4, "Толщина платы: ",input,Thickness#
ClearMrk=1
AddForm #1, 10, 5, "Очищать маркировку автоматически", Check, ClearMrk
AddSpitoon=0
AddForm #1, 10, 6, "Добавить плевательницу на технологическом поле", Check, AddSpitoon
WideBorderDPP=1
if mpp = -1 then
	AddForm #1, 10, 7, "Редактировать рамку ДПП", Check, WideBorderDPP
end if
DisplayForm #1
Canceled% = formcancel(1)
DeleteForm #1
if Canceled% = 1 then goto endofscript

if rcn=-1 then OneSide=rsn
if rsn=-1 then OneSide=rcn

For lay = Nextlay to Highestlayer!
	setlayer@ lay
	if lay <> comp and lay <> solder and lay <> mc and lay <> rcn and lay <> rsn and lay <> ms and lay <> drill and lay <> mill  then 
		edit_removelyr@ lay
	end if
next

'-------------------- для ДПП отдельная подпрограмма
if mpp = -1 then 
	gosub MakeDPP
	if DPPerror then
		goto endofscript
	else 
		goto MakeSpitoons
	end if
end if


if OneSide = -1 then
	setlayer@ rcn
else 
	setlayer@ OneSide
end if
layer_alloff@ 1
if vismaxx! = Sizes#(Small,X) then
	Block=Small
	TargLeftX# = 32.5
	TargBotY# = 25.0
	TargRightX# = 197.5
	TargTopY# = 370.0
	'Print "Small block"
elseif vismaxx! = Sizes#(Middle,X) then
	Block=Middle
	TargLeftX# = 32.5
	TargBotY# = 25.0
	TargRightX# = 252.5
	TargTopY# = 370.0
	'Print "Middle block"
elseif vismaxx! = Sizes#(Pferd,X) then
	Block=Pferd
	TargLeftX# = 0
	TargBotY# = 0
	TargRightX# = 0
	TargTopY# = 0
	'Print "Das Pferd. Nicht arbeiten !!!"
else
	Print "Странный размер по X :", vismaxx!
	goto endofscript
end if

'-------------------- пытаемся определить есть ли прицелы на маркировке
if OneSide = -1 then 
	setlayer@ rcn
	layer_alloff@ 1
	setlayer@ rsn
else
	setlayer@ OneSide
	layer_alloff@ 1
end if
TempLayer = Blanklayer!
edit_layer@ TempLayer,-2,-2,4,0,"Temp"
edit_copy@
edit_multiple@
axy@ Targets#(Block,X,Right),Targets#(Block,Y,Bot)
axy@ Targets#(Block,X,Left),Targets#(Block,Y,Top)
edit_multiple@
setcoplayer@ TempLayer,1
copy_to_layer@
back@
setlayer@ TempLayer
layer_alloff@ 1
NoSilkTargets=Layerblank!
edit_delete@
edit_selectall2@
delete_edits@ 4,1
back@

if ClearMrk = 0 then goto wo_erasing
'-----------пытаемся определить есть ли данные между реперами на маркировке 
if OneSide = -1 then 
	setlayer@ rcn
	layer_alloff@ 1
	setlayer@ rsn
else
	setlayer@ OneSide
	layer_alloff@ 1
end if

edit_copy@
edit_group@
edit_inside@
edit_cross@
axy@ 36.0,22.0
axy@ Targets#(Block,X,Right)-5,32.5
axy@ 36.0,Sizes#(Block,Y)-32,5
axy@ Sizes#(Block,X)/2,Sizes#(Block,Y)-20,5
back@
setcoplayer@ TempLayer,1
copy_to_layer@
back@
setlayer@ TempLayer
IsBlank=Layerblank!
edit_removelyr@ TempLayer
if IsBlank = 0 then 
	MessageBox  "Возможно плата слишком велика и часть маркировки может быть случайно удалена."+CR!+"Очистить автоматически?" ,"Проблема", YESNOCANCEL,Answer 
	if Answer = 2 then 
		goto endofscript
	elseif Answer = 7 then 
		ClearMrk=0
		goto wo_erasing 
	end if
end if

'-------------------------- удаляем ненужное с маркировки --------------
'-------------- rcn
if rcn = -1 then goto clear_rsn
RcnClr = Blanklayer!
edit_layer@ RcnClr,-2,-2,4,0,"RcnClr"
setlayer@ rcn
layer_alloff@ 1
edit_move@
edit_group@
edit_outside@
edit_cross@
axy@ 20.0, 22,0
axy@ Sizes#(Block,X)-21.0,Sizes#(Block,Y)-23.0
back@
movetolayer@ RcnClr
back@
' удаляем реперы и номер
edit_move@
edit_group@
edit_inside@
edit_cross@
'реперы
axy@ 22.0,22.0
axy@ 28.0,28.0
axy@ Sizes#(Block,X)-22.0,22.0
axy@ Sizes#(Block,X)-28.0,28.0
axy@ 22.0,Sizes#(Block,Y)-22.0
axy@ 28.0,Sizes#(Block,Y)-28.0
axy@ Sizes#(Block,X)-22.0,Sizes#(Block,Y)-22.0
axy@ Sizes#(Block,X)-28.0,Sizes#(Block,Y)-28.0
' номер
axy@ 41.0,Sizes#(Block,Y)-32.0
axy@ Sizes#(Block,X)-37.0,Sizes#(Block,Y)-19.0
back@
movetolayer@ RcnClr
back@
if rsn = -1 then goto wo_erasing

'---------- clear rsn
clear_rsn:
RsnClr = Blanklayer!
edit_layer@ RsnClr,-2,-2,4,0,"RsnClr"
setlayer@ rsn
layer_alloff@ 1
edit_move@
edit_group@
edit_outside@
edit_cross@
axy@ 20.0, 22,0
axy@ Sizes#(Block,X)-21.0,Sizes#(Block,Y)-23.0
back@
movetolayer@ RsnClr
back@
' удаляем реперы и номер
edit_move@
edit_group@
edit_inside@
edit_cross@
'реперы
axy@ 22.0,22.0
axy@ 28.0,28.0
axy@ Sizes#(Block,X)-22.0,22.0
axy@ Sizes#(Block,X)-28.0,28.0
axy@ 22.0,Sizes#(Block,Y)-22.0
axy@ 28.0,Sizes#(Block,Y)-28.0
axy@ Sizes#(Block,X)-22.0,Sizes#(Block,Y)-22.0
axy@ Sizes#(Block,X)-28.0,Sizes#(Block,Y)-28.0
' номер
axy@ 41.0,Sizes#(Block,Y)-32.0
axy@ Sizes#(Block,X)-37.0,Sizes#(Block,Y)-19.0
back@
movetolayer@ RsnClr
back@

'-----------------------------------------------------------

wo_erasing:

'------------------  сжимаем таблицу и добавляем аппертуру для прицелов 
aptable_compress@
update_dcodebar@
TargApr = Highestdcode! + 1
set_aperture@ TargApr,"Donut rnd od:2.4 id:2.0"
setdcode@ TargApr
update_dcodebar@

'----------------- добавляем прицелы, если их нет на маркировке
if NoSilkTargets then 
	if OneSide=-1 then
		setlayer@ rcn
		layer_alloff@ 1
		add_flash@
		axy@ Targets#(Block,X,Left),Targets#(Block,Y,Bot)
		axy@ Targets#(Block,X,Right),Targets#(Block,Y,Bot)
		axy@ Targets#(Block,X,Left),Targets#(Block,Y,Top)
		axy@ Targets#(Block,X,Right),Targets#(Block,Y,Top)
		setlayer@ rsn
		layer_alloff@ 1
		axy@ Targets#(Block,X,Left),Targets#(Block,Y,Bot)
		axy@ Targets#(Block,X,Right),Targets#(Block,Y,Bot)
		axy@ Targets#(Block,X,Left),Targets#(Block,Y,Top)
		axy@ Targets#(Block,X,Right),Targets#(Block,Y,Top)
		back@
	else
		setlayer@ OneSide
		layer_alloff@ 1
		add_flash@
		axy@ Targets#(Block,X,Left),Targets#(Block,Y,Bot)
		axy@ Targets#(Block,X,Right),Targets#(Block,Y,Bot)
		axy@ Targets#(Block,X,Left),Targets#(Block,Y,Top)
		axy@ Targets#(Block,X,Right),Targets#(Block,Y,Top)
		back@
	end if
end if
'-------------создаем слой targets и добавляем прицелы
TargLayer = Blanklayer!
edit_layer@ TargLayer,-2,-2,4,0,"targets.gbr"
setlayer@ TargLayer
layer_alloff@ 1
add_flash@
axy@ Targets#(Block,X,Left),Targets#(Block,Y,Bot)
axy@ Targets#(Block,X,Right),Targets#(Block,Y,Bot)
axy@ Targets#(Block,X,Left),Targets#(Block,Y,Top)
axy@ Targets#(Block,X,Right),Targets#(Block,Y,Top)
back@

'--------------------создаем зеркальную копию нижней маркировки
if  rsn <> -1 then 
	RsnMir=Blanklayer!
	edit_layer@ RsnMir,-2,-2,4,0,"RsnMir.gbr"
	setlayer@ rsn
	layer_alloff@ 1
	edit_copy@
	edit_selectall2@
	setcoplayer@ RsnMir,1
	copy_to_layer@
	back@
	setlayer@ RsnMir
	layer_alloff@ 1
	edit_mirv@
	setmirroraxis@ 2
	edit_selectall2@
	axy@ Sizes#(Block,X)/2.0,Sizes#(Block,Y)/2.0
	back@
end if

'----------------- поворот 
layer_alloff@ 0 
edit_rotate@
setrot100angle@ 27000
edit_selectall2@
axy@ Sizes#(Block,X)/2.0,Sizes#(Block,Y)/2.0
back@
'------------------- сдвиг 
setsnap@ 1
LeftX#=Sizes#(Block,X)/2.0-Sizes#(Block,Y)/2.0
BotY#=Sizes#(Block,Y)/2.0-Sizes#(Block,X)/2.0
edit_move@
edit_selectall2@
axy@ LeftX#, BotY#
setsnap@ 0
if Block=Small then 
	axy@ 112.5,61.0
elseif Block=Middle then 
	axy@ 112.5,0.0
else
	axy@ 17.5,0.0
end if
back@
'--------------------------- плевательницы
MakeSpitoons:
if AddSpitoon then
	if mpp = -1 then
		SpitoonY#=TargTopY#+5
		SpitoonX1#=TargLeftX#+15
		SpitoonX2#=TargRightX#-15
	else
		SpitoonY#=RotatedTargets#(Block,Y,Top)+4
		SpitoonX1#=RotatedTargets#(Block,X,Left)+15
		SpitoonX2#=RotatedTargets#(Block,X,Right)-15
	end if
	Apr = Highestdcode! + 1
	set_aperture@ Apr,"Round d:0.15"
	setdcode@ Apr
	update_dcodebar@
	DIM MarkLayers#(1)
	MarkLayers#(0)=rcn
	if rsn <> -1 then
		MarkLayers#(1)=RsnMir
	else 
		MarkLayers#(1)=-1
	end if
	for a#=0 to 1
		if MarkLayers#(a#)<> -1 then
			setlayer@ MarkLayers#(a#)
			add_line@
			for b#=0 to 10
				axy@ SpitoonX1#,SpitoonY#+b#*0.5
				axy@ SpitoonX2#,SpitoonY#+b#*0.5
				back@
			next
			back@
 		end if
	next
end if
'--------------------------- экспорт файлов 
OutputFiles:

if CustExist=0 then
	MKDIR OutCustom$
end if
if BrdExist=0 then
	MKDIR Outpath$
end if

setup_fmtype@ 0,1
setup_fmtunit@ 0,1
setup_fmtdigits@ 0,3,3
setup_fmtmode@ 0,0
setup_fmtzero@ 0,0
if rcn <> -1 then 
	set_layermirrorstate@ rcn, 0
	layer_setimagepolarity@ rcn, 0
	exportgbrfile@ rcn,-1,Outpath$+"\"+name$+"_comp.gbr"
end if
if rsn <> -1 then exportgbrfile@ RsnMir,-1,Outpath$+"\"+name$+"_solder.gbr"
exportgbrfile@ TargLayer,-1,Outpath$+"\"+name$+"_target.gbr"

'----------определяем толщину заготвки и название набора прицелов----------------
Thickness# = Thickness#+0.85
if mpp <> -1 then
	if Block=Small then
		SetFiducials$="SET=MPP230"
	elseif Block = Middle then
		SetFiducials$="SET=MPP280"
	else
		SetFiducials$="SET=MPP485"
	end if
else
	setlayer@ 1
	SetFiducials$=FMTUSING$("SET=DPP###x###",Vismaxy!-Visminy!,Vismaxx!-Visminx!)
end if
'------------------------выводим inf файлы---------------------------
DIM side$(2)
side$(0)="_comp.inf"
side$(1)="_solder.inf"
side$(2)="_target.inf"
for i=0 to 2
	if (i=0 and rcn=-1) or (i=1 and rsn=-1) then next
	OPEN Outpath$+"\"+name$+side$(i) for output as #1
	print #1, "[Setup]"
	print #1, using "DI=##.##",Thickness#
	print #1, SetFiducials$
	if mpp = -1 then
		print #1,";Targets:"
		print #1, using ";Left bottom ##.## , ##.##",TargLeftX#, TargBotY# 
		print #1, using ";Left top ##.## , ##.##",TargLeftX#, TargTopY# 
		print #1, using ";Right top ##.## , ##.##",TargRightX#, TargTopY# 
		print #1, using ";Right bottom ##.## , ##.##",TargRightX#, TargBotY# 
	end if
	close #1
next
'-------------------------------------------------------

FinishMSG:
Finish$="Файлы были сохранены в "+Outpath$+CR!
if ClearMrk then
	If rcn <> -1 then
		setlayer@ rcn
		layer_alloff@ 1
		Finish$=Finish$+"Рекомендуем ознакомиться с содержимым слоя RcnClr"+CR!
		if rsn <> -1 then
			setlayer@ rsn
			Finish$=Finish$+"и слоя RsnClr"
		end if
	else
		setlayer@ rsn
		layer_alloff@ 1
		Finish$=Finish$+"Рекомендуем ознакомиться с содержимым слоя RsnClr"+CR!
	end if
end if
MessageBox Finish$, "Финиш",INFORMATION
'-------------------end of script ----------------------------
endofscript:
end
'----------------------------------------------------------------------------
'------------------------------ о б р а б о т к а ДПП -----------------------
'----------------------------------------------------------------------------
MakeDPP:
RotateFlg=0
if OneSide = -1 then
	setlayer@ rcn
else 
	setlayer@ OneSide
end if
layer_alloff@ 1
SizeX#= vismaxx! 
SizeY#= vismaxy! 
'if (SizeX# < 300) AND (SizeY# < 300) then
if (SizeX# < 280) AND (SizeY# < 280) then
	MessageBox  "Блок слишком мал"+CR!+"Ширина блока должна быть не менее 300 мм", "Ошибка",ERROR 
	DPPerror =1
	return
end if
'-------удаление слишком широкой рамки на ДПП ----
layer_alloff@ 0
if WideBorderDPP AND (visminx!<0) AND (visminy!<0) then
for i=1 to 2
	For lay = 0 to Highestlayer! 
		setlayer@ lay
		edit_delete@
		axy@ SizeX#,SizeY#
		back@
	next
	SizeX#= vismaxx! 
	SizeY#= vismaxy! 
	layer_alloff@ 0
next
end if
'----------------- rotate and move 
if SizeX# < SizeY# then 
	layer_alloff@ 0 
	edit_rotate@
	setrot100angle@ 27000
	edit_selectall2@
	axy@ SizeX#/2.0,SizeY#/2.0
	back@
	edit_move@
	edit_selectall2@
	axy@ visminx!,visminy!
	axy@ 0.0,0.0
	back@
	swap SizeX#,SizeY#
	RotateFlg=1
end if


'suspend

TargLeftX# = 5.5
TargBotY# = 12.5.0
TargRightX# = SizeX# - 5.5
TargTopY# = SizeY# - 20.0
'---- проверяем прицелы на comp-------------
setlayer@ comp
layer_alloff@ 1
nTrg=0
query_dcode@
axy@ TargLeftX#,TargBotY#
nTrg=nTrg+Queryfound!
axy@ TargRightX#,TargBotY#
nTrg=nTrg+Queryfound!
axy@ TargRightX#,TargTopY#
nTrg=nTrg+Queryfound!
axy@ TargLeftX#,TargTopY#
nTrg=nTrg+Queryfound!
back@
'DCODE=Activedcode!
'break
if nTrg = 0 then 
	MessageBox  "Отсутствуют прицелы на слое top", "Ошибка",ERROR 
	DPPerror =1
	return
elseif nTrg <> 4 then
	MessageBox  "Неверное число прицелов на слое top", "Ошибка",ERROR 
	DPPerror =1
	return
end if
'---- проверяем прицелы на маркировке-------------
if OneSide = -1 then
	setlayer@ rcn
else
	setlayer@ OneSide
end if

layer_alloff@ 1

nTrg=0
query_dcode@
axy@ TargLeftX#,TargBotY#
nTrg=nTrg+Queryfound!
axy@ TargRightX#,TargBotY#
nTrg=nTrg+Queryfound!
axy@ TargRightX#,TargTopY#
nTrg=nTrg+Queryfound!
axy@ TargLeftX#,TargTopY#
nTrg=nTrg+Queryfound!
back@
DCODE=Activedcode!
if nTrg = 0 then 
	MessageBox  "Отсутствуют прицелы на маркировке", "Ошибка",ERROR 
	DPPerror =1
	return
elseif nTrg <> 4 then
	MessageBox  "Неверное число прицелов на маркировке", "Ошибка",ERROR 
	DPPerror =1
	return
end if
if Dcodeshape! <> 7 then
	MessageBox  "Неправильная форма КП для прицелов на маркировке", "Ошибка",ERROR 
	DPPerror =1
	return
end if

TargLayer = Blanklayer!
edit_layer@ TargLayer,-2,-2,4,0,"targets.gbr"
setlayer@ TargLayer
layer_alloff@ 1
add_flash@
axy@ TargLeftX#, TargBotY#
axy@ TargRightX#,TargBotY#
axy@ TargLeftX#, TargTopY#
axy@ TargRightX#,TargTopY#
back@

'suspend
if ClearMrk = 0 then 
	if rsn <> -1 then goto MirrorRsnDPP
	goto continueDPP
end if
'-------------------------- удаляем ненужное с маркировки --------------
'-------------- rcn
if rcn = -1 then goto clear_rsnDPP
RcnClr = Blanklayer!
edit_layer@ RcnClr,-2,-2,4,0,"RcnClr"
setlayer@ rcn
layer_alloff@ 1
edit_move@
edit_group@
edit_outside@
edit_cross@
axy@ 17.0, 17.0
axy@ SizeX# - 17.5,SizeY# - 24.5
back@
movetolayer@ RcnClr
back@
'-------------- добавляем удаленные прицелы
add_flash@
axy@ TargLeftX#, TargBotY#
axy@ TargRightX#,TargBotY#
axy@ TargLeftX#, TargTopY#
axy@ TargRightX#,TargTopY#
back@

'----------------------------- rsn 
clear_rsnDPP:
if rsn = -1 then goto continueDPP
RsnClr = Blanklayer!
edit_layer@ RsnClr,-2,-2,4,0,"RsnClr"
setlayer@ rsn
layer_alloff@ 1
edit_move@
edit_group@
edit_outside@
edit_cross@
axy@ 17.0, 17.0
axy@ SizeX# - 17.0,SizeY# - 24.5
back@
movetolayer@ RsnClr
back@
'-------------- добавляем удаленные прицелы
add_flash@
axy@ TargLeftX#, TargBotY#
axy@ TargRightX#,TargBotY#
axy@ TargLeftX#, TargTopY#
axy@ TargRightX#,TargTopY#
back@
'--------------------создаем зеркальную копию нижней маркировки
MirrorRsnDPP:
RsnMir=Blanklayer!
edit_layer@ RsnMir,-2,-2,4,0,"RsnMir.gbr"
setlayer@ rsn
layer_alloff@ 1
edit_copy@
edit_selectall2@
setcoplayer@ RsnMir,1
copy_to_layer@
back@
setlayer@ RsnMir
layer_alloff@ 1
edit_mirv@
setmirroraxis@ 1
edit_selectall2@
axy@ SizeX#/2.0,SizeY#/2.0
back@
'-------------------- сдвиг ---------------------
continueDPP:
YmoveDPP#=0
if (SizeY# > 180.0) AND (SizeY# < 250.0) then
	YmoveDPP# = YmoveDPP#+61.0
end if
XmoveDPP#= 310.0-SizeX#/2.0
layer_alloff@ 0
edit_move@
edit_selectall2@
axy@ 0.0, 0.0
axy@ XmoveDPP#,YmoveDPP#
back@
TargLeftX# =TargLeftX# + XmoveDPP#
TargRightX#=TargRightX#+ XmoveDPP#
TargBotY# = TargBotY#+YmoveDPP#
TargTopY# = TargTopY#+YmoveDPP#
'---------- сжимаем таблицу апертур ----------------------------
aptable_compress@
update_dcodebar@
return
'---------------------

!include "translit.inc" 

