util_camed@
setgridsnap@ 0
setsnap@ 0

dim d#(200)'diametr
dim if#(200)'in feed
dim of#(200)'out feed
dim r#(200)'rpms
dim numbers(200) ' использованые номера сверел
dim sizes#(200) ' использованые номера сверел
dim frezy(200)
dim drr(10)
dim mir(10)
fre = 1

tablepath$="x:\tool\camscripts"

X% = dbmaxx!
Y% = dbmaxy!
' 16-01-2015 когда добавились в двусторонних 5мм  с каждой стороны нужно правильно считать размер заготовки'
if dbminx!=-5 then
	X%=X%-5
	Y%=Y%-5
end if
if X%<Y% then vert%=1
tochnovert%=-1
drillnew = -1
dopdrill = -1
scalex# = 1
scaley# = 1

xprob# = 10 ' все! это пробивка для фрезеровки
yprob# = (Y%-180)/2.0
mk2exist = 0 ' 18-02-2020 больше нет ' есть двухшпиндельная сверловка
mk4exist = 0 ' 18-02-2020 больше нет ' есть четырехшпиндельная сверловка
ex2exist = 1 ' есть сверловка для экселона специально ниже перехода мимо формы чтобы правильно работало формирование s32 в надевании рамки// 07-04-2017 удрвл вывод в надевании
frzexist = 0 ' 18-02-2020 больше нет
mx1exist = 0 ' 18-02-2020 больше нет

' нужно хоть один файл обозначить
tablefile$ = tablepath$+"\tables4.lst"

if drillname$ <> "" then goto noform

!INCLUDE "getpathandname.scr"
!include "layersset.scr"

' проверка на поворот, если в 0,0 есть флешь d10 значит точно не надо поворачивать, и если в 0,dbmaxx! есть то тоже точно не надо
if comp<>-1 then
	setlayer@ comp
	layer_alloff@ 1
	query_dcode@
	axy@ 0,0
	back@
	break
	if Activedcode!=10 then
		tochnovert%=10;
		vert%=0
	else
		query_dcode@
		axy@ dbmaxx!,0
		back@
		if Activedcode!=10 then
			tochnovert=10;
			vert%=1
		else
			tochnovert%=abs(X%-Y%)*100/X% ' процентное отношение сторон'
			
			' не известно точно ли меньше 10 процентов будем спрашивать'
			
		end if
	end if
end if

' определение имени
if fileexists(path$+"copper.rpt") then 
	open path$+"copper.rpt" for input as #1
		input #1,drillname$
	close #1
	if len(drillname$) > 8 then
		MACRO_PLAY "createcoppers.scr"
		drillname$ = name$
	end if
else 
	MACRO_PLAY "createcoppers.scr"
	drillname$ = name$
end if

' вычисление кэфициэнтов
if 1=1 then
	if mpp = -1 then
		s32name$ = "dpp.s32"
	else
		s32name$ = "mpp"+str$(layers)+".s32"
		if not fileexists(path$+s32name$) then
			s32name$ = "mpp.s32"
		end if
	end if

	dim xscal#(30)
	dim yscal#(30)
	xmin# = 1000
	Ymin# = 1000
	xmax# = 0
	ymax# = 0

	if fileexists(path$+s32name$) then 
		open path$+S32name$ for input as #1
			do UNTIL EOF (1)
			input #1,hhh$
			if left$(hhh$,1) = "A" then
				num# = val(MID$(hhh$,2))
				input #1,hhh$
				xscal#(num#) = val(MID$(hhh$,INSTR(1,hhh$," ")))
			end if
			if left$(hhh$,1) = "B" then
				num# = val(MID$(hhh$,2))
				input #1,hhh$
				yscal#(num#) = val(MID$(hhh$,INSTR(1,hhh$," ")))
			end if
			LOOP
		close #1
		for num%=2 to layers%+1
			if ymin# > yscal#(num%) AND yscal#(num%)>0 then
				ymin# = yscal#(num%)
			end if
			if ymax# < yscal#(num%) AND yscal#(num%)>0 then
				ymax# = yscal#(num%)
			end if
			if xmin# > xscal#(num%) AND xscal#(num%)>0 then
				xmin# = xscal#(num%)
			end if
			if xmax# < xscal#(num%) AND xscal#(num%)>0 then
				xmax# = xscal#(num%)
			end if
		next
		scalex# = (xmin#+xmax#)/2.0/xscal#(1)
		scaley# = (ymin#+ymax#)/2.0/yscal#(1)
	end if
end if
' Конец вычисления коэффициэнтов'

' самая новая пробивка
if mpp <> -1 then
	if X% = 280 and Y% = 395 then
		sz280x395 = 1
	end if

	if X% = 485 and Y% = 585 then
		sz485x585 = 1
		mk2exist = 0 ' нет двухшпиндельной сверловки
	end if

	if X% = 230 and Y% = 395 then
		sz230x395 = 1
	end if
end if

if scalex#>1.0005 or scalex# < 0.9995 or scaley#>1.0005 or scaley#<0.9995 then
	messagebox "Couriosity scale koeff X="+str$(scalex#)+", Y="+str$(scaley#),"Are you shure?",OKCANCEL,MacroEnd%
	if MacroEnd% = 2 then end
end if

if scalex#>1.001 or scalex# < 0.998 or scaley#>1.001 or scaley#<0.998 then
	messagebox "I shell not scaling!!!","Must be only one!",OKCANCEL,MacroEnd%
	if MacroEnd% = 2 then end
	scalex#=1
	scaley#=1
end if

frr = 0
closefile% = 1

DPP:
OpenForm #1,70,7,"Mill and drill (c) Sch'arapoff", character, okcancel
if mpp <> -1 then
	frr=0
	vert% = 1
else
	' На кондукторах нужно выбирать
	if (comp% = -1 AND solder% = -1) then
		tochnovert% = 0
	end if
	if tochnovert%<9 then
		AddForm #1, 0, 0, "Vertical?",check,vert%
	end if
end if

AddForm #1, 50, 0, "Close after end?",check,closefile%


AddForm #1, 0, 1, "Drills:",print
for i = 1 to drs
	shiftx = 13+(i-1)*(55/drs)
	drr(i) = 1
	AddForm #1, shiftx, 1, dr$(i),check,drr(i)
next
AddForm #1, 0, 2, "Mills:",print
for i = 1 to mis
	shiftx = 20+(i-1)*20
	mir(i) = 1
	AddForm #1, shiftx, 2, mi$(i),check,mir(i)
next
AddForm #1, 0, 3, "Name (for drill)",input,drillname$
AddForm #1,0, 5, "scale X", input,scalex#
AddForm #1,35, 5, "scale Y", input,scaley#
DisplayForm #1
Canceled% = formcancel(1)
DeleteForm #1

if Canceled% = 1 then goto mkrfrz_endscript

noform:
print_msg "I am working... wait!!!"

' развернуть если вертикальное
if vert% <> 1 then
	'view_all@
	layer_alloff@ 0
	edit_rotate@
	setsnap@ 0
	edit_selectall2@ 
	axy@ 0,Y%
	back@
	end@
	edit_move@
	edit_selectall2@ 
	axy@ 0,Y%
	axy@ 0.0000,0.0000
	back@
	temp = Y%
	Y% = X%
	X% = temp
	vert% = 1
	
	' поменять сжаттие местами'
	temp# = scalex#
	scalex# = scaley#
	scaley# = temp#
end if
' --- конец развернуть --- '


'1. создание файлов заголовка, должны быть до сверления под фрезеровку потому что вычисляется Z
if 1=1 then
	if mpp <> -1 then
		Z# = yprob#
	end if 

	fr = 1
	head$= path$ + "headerf.hdr"
	gosub createheader 
	fr=0

	hh = 4; ' 4 шпинделя
	head$= path$ + "header4.hdr"
	gosub createheader 

	head$= path$ + "header2.hdr"
	hh = 2; ' 2 шпинделя
	gosub createheader 
end if
' -- конец файлов заголовка -- '

if drs > 0 then
	for drlay = 1 to drs
		if drr(drlay) = 0 then
			drill = -1
		else
			drill = dr(drlay)
		end if
		if drlay <> 1 then
			suffix$="-"+str$(drlay)
		end if
		if drill <> -1 then
			if scalex# <> 1 OR scaley# <> 1 then 
				drillnew = Blanklayer!
				setlayer@ drill
				layer_alloff@ 1
				' сместить
				edit_move@
				edit_selectall2@
				axy@ 0,0
				axy@ -X%/2,-Y%/2
				back@
				' сжать
				edit_scale@ drill,drillnew,scalex#*100000000,scaley#*100000000
				' layer_alloff@ 0
				drill = drillnew
				' сместить на место
				edit_move@
				edit_selectall2@
				axy@ -X%/2,-Y%/2
				axy@ 0,0
				back@
			end if

			if mk2exist then
				open path$+drillname$+".mk2.old" for APPEND as #1
					print #1,"Этот файлы были сжаты САМ средствами. Обязательно вывести новый!!!"
					print #1,Time!+"   "+str$(scalex#)+"   "+str$(scaley#)
				close #1
			end if
			if mk4exist then
				open path$+drillname$+".mk4.old" for APPEND  as #1
					print #1,"Этот файлы были сжаты САМ средствами. Обязательно вывести новый!!!"
					print #1,Time!+"   "+str$(scalex#)+"   "+str$(scaley#)
				close #1
			end if
			if ex2exist then
				open path$+drillname$+".ex2.old" for APPEND  as #1
					print #1,"Этот файлы были сжаты САМ средствами. Обязательно вывести новый!!!"
					print #1,Time!+"   "+str$(scalex#)+"   "+str$(scaley#)
				close #1
			end if
		end if

		' Добавить сверловку под фрезеровку если двусторонняя
		' а вот если многослойная добавлять под фрезеровку отвертия, так как они не должны сжиматься'
		' вот вопрос как убрать со старых файлов?'
		
		' для начала создадим доп слой в любом случае'
		if 1=1 then
			'Highest tool table number
			HighestNCT%=-1
			setlayer@ 1
			for lay=1 to Highestlayer!
				setlayer@ lay
				if Layertype! = 21 and Tooltable! > HighestNCT% then 
				HighestNCT%=Tooltable!
				end if
			next
			for tab = HighestNCT%+1 to 100
				'set_nc_table@ tab
				'if dbq_tooltable! = tab then
					nc_delete_table@ tab
				'end if
			next
			dopdrill = Blanklayer!
			doptable = HighestNCT%+1
			nc_delete_table@ doptable
			nc_add_table@ doptable
			nc_set_table_type@ doptable,3
			nc_set_table_name@ doptable,"Dop Drill Tool"
			nc_add_tool@ doptable,1
			nc_set_tool_id@ doptable,1,1
			nc_set_tool_type@ doptable,1,3
			nc_set_tool_size@ doptable,1,3.0000
			'util_nced@
			setsnap@ 0
			edit_layer@ dopdrill,-2,-2,21,0
			nc_assign_tool_table_to_layer@ dopdrill,doptable
			nc_set_layer_rank@ dopdrill,1
		end if
		' создали доп слой
		
		' это если многослойка
		' удалить отверстия, ведь они уже сжаты'
		if mpp <> -1 then
			setlayer@ drill
			layer_alloff@ 1
			edit_delete@
			edit_group@
			axy@ 10.0000-5.0,Z#-5.0
			axy@ 10.0000+5.0,Z#+45+90+45+5.0
			back@
			delete_edits@ 4,1
			back@
		end if

		setlayer@ drill
		selected_layer = drill

		if drill <> -1 and mx1exist then
			' минима
			'07-04-2017 это минима, на ней не сверлятся отверстия для фрезеровки потому что на ней и фрезеруется
			' вывод 
			expdrl_generate_Header@ 1
			export_XYmodal@ 1,0
			nc_exp_drill_t00@ 0
			expdrl_generate_Tools@ 1
			setup_fmtype@ 1,5
			setup_fmtunit@ 1,1
			setup_fmtdigits@ 1,3,3
			setup_fmtmode@ 1,0
			nc_exp_drilltool_infeed@ 0
			nc_exp_drilltool_retract@ 0
			nc_exp_drilltool_RPMs@ 0
			nc_exp_drilltool_MaxHits@ 0
			nc_exp_drill_Exc_decimals@ 1
			nc_exp_drill_Exc_toolingoffset@ 1
			''
			setup_fmtype@ 2,5
			setup_fmtunit@ 2,1
			setup_fmtdigits@ 2,3,3
			setup_fmtmode@ 2,0
			setup_fmtzero@ 2,2
			nc_exp_mill_IncludeDrills@ 1
			nc_exp_mill_Exc_decimals@ 1
			nc_exp_mill_Exc_toolingoffset@ 1
			nc_exp_milltool_infeed@ 0
			nc_exp_milltool_retract@ 0
			nc_exp_milltool_RPMs@ 0
			tablefile$ = tablepath$+"\tables4.lst"
			minimafr = 1
			GOSUB corecttool
			minimafr = -1
			drext$="mx1"
			mill_out@ drill,path$+drillname$+suffix$+"1."+drext$ ' для плат в металлизированной фрезеровкой'
		end if

		' теперь если это excelon или многослойка поставим отверстия в 10 мм от края для фрезеровки'
		'7-04-2017 теперь добавляем для всех видов сверловки потому, что все сверлятся на двух штифтах'
		setlayer@ dopdrill
		layer_alloff@ 1
		nc_add_drill_hit@
		setsnap@ 0
		if Z# < Y% then
			axy@ 10.0000,Z#
		end if
		if Z#+45 < Y% then
			axy@ 10.0000,Z#+45
		end if
		if Z#+45+90 < Y% then
			axy@ 10.0000,Z#+45+90
		end if
		if Z#+45+90+45 < Y% then
			axy@ 10.0000,Z#+45+90+45
		end if
		back@
		edit_move@
		edit_selectall2@ 
		movetolayer@ drill
		back@
		' конец сверления отв под фрезеровку '


		if drill <> -1 and mk4exist then
			tablefile$ = tablepath$+"\tables4.lst"
			selected_layer = drill
			GOSUB corecttool

			' чтобы влючить фрезеровку поставил сюда текст из фрезеровки

			'4. Настройка вывода mill mashine
			setup_fmtype@ 2,7
			'mill машина - SIEB & MEYER 3000
			setup_fmtunit@ 2,1
			'mill mashine - metric
			setup_fmtdigits@ 2,3,3
			'mill mashine - 3.3 digit
			setup_fmtmode@ 2,0
			'mill mashine - absolute
			setup_fmtzero@ 2,2
			'mill mashine - zero none
			head$= path$ + "header4.hdr"
			mill_sethdrfile@ head$
			'header file
			nc_exp_drill_Exc_toolingoffset@ 0
			nc_exp_mill_Exc_decimals@ 1
			nc_exp_mill_IncludeDrills@ 1
			Expmill_generate_Header@ 0
			nc_exp_drilltool_infeed@ 1
			nc_exp_drilltool_retract@ 1
			nc_exp_drilltool_RPMs@ 1
			nc_exp_drilltool_MaxHits@ 1
			nc_exp_milltool_infeed@ 1
			nc_exp_milltool_retract@ 1
			nc_exp_milltool_RPMs@ 1


			'5. Собственно вывод фрезеровки
			millPCB_out@ drill,path$+drillname$+suffix$+"1.mk4"
			'	mill_out@ mill,path$+name$+"1.frz"
			Expmill_generate_Header@ 1

		end if

		if drill <> -1 and mk2exist then
			tablefile$ = tablepath$+"\tables2.lst"
			selected_layer = drill
			GOSUB corecttool

			'2. Настройка вывода drill mashine

			setup_fmtype@ 1,7
			'дрилл машина - SIEB & MEYER 3000
			setup_fmtunit@ 1,1
			'drill mashine - metric
			setup_fmtdigits@ 1,3,3
			'drill mashine - 3.3 digit
			setup_fmtmode@ 1,0
			'drill mashine - absolute
			setup_fmtzero@ 1,2
			'drill mashine - zero none
			head$= path$ + "header2.hdr"
			drill_sethdrfile@ head$
			'header file
			nc_exp_drill_Exc_toolingoffset@ 0
			nc_exp_drill_Exc_decimals@ 1
			Expdrl_generate_Header@ 0
			nc_exp_drilltool_infeed@ 1
			nc_exp_drilltool_retract@ 1
			nc_exp_drilltool_RPMs@ 1
			nc_exp_drilltool_MaxHits@ 1
			nc_exp_milltool_infeed@ 1
			nc_exp_milltool_retract@ 1
			nc_exp_milltool_RPMs@ 1


			'3. Собственно вывод сверловки
			drillPCB_out@ drill,path$+drillname$+suffix$+"1.mk2"
			'	drill_out@ drill,path$+name$+"1.mk2"
			Expdrl_generate_Header@ 1
		end if

		' экселон
		if drill <> -1 and ex2exist then
			minimafr = 1
			GOSUB corecttool
			minimafr = -1

			drext$="ex2"
			''
			setlayer@ drill
			' вывод 
			expdrl_generate_Header@ 1
			export_XYmodal@ 1,0
			nc_exp_drill_t00@ 0
			expdrl_generate_Tools@ 1
			setup_fmtype@ 1,5
			setup_fmtunit@ 1,1
			setup_fmtdigits@ 1,3,3
			setup_fmtmode@ 1,0
			nc_exp_drilltool_infeed@ 0
			nc_exp_drilltool_retract@ 0
			nc_exp_drilltool_RPMs@ 0
			nc_exp_drilltool_MaxHits@ 0
			nc_exp_drill_Exc_decimals@ 1
			nc_exp_drill_Exc_toolingoffset@ 1
			''
			setup_fmtype@ 2,5
			setup_fmtunit@ 2,1
			setup_fmtdigits@ 2,3,3
			setup_fmtmode@ 2,0
			setup_fmtzero@ 2,2
			nc_exp_mill_IncludeDrills@ 1
			nc_exp_mill_Exc_decimals@ 1
			nc_exp_mill_Exc_toolingoffset@ 1
			nc_exp_milltool_infeed@ 0
			nc_exp_milltool_retract@ 0
			nc_exp_milltool_RPMs@ 0
			''
			'drillPCB_out@ drill,path$+drillname$+"1.ex2"
			mill_out@ drill,path$+drillname$+suffix$+"1."+drext$ ' для плат в металлизированной фрезеровкой'
		end if

		'6. дополнение файла сверловки
		close_msg
		print_msg "Correct files." + CR! + "It is so long." + CR! + "Wait!!!"

		if drill <> -1 then
			'Вернем коректировку к оригинальной'
			tablefile$ = tablepath$+"\tables4.lst"
			selected_layer = drill
			GOSUB corecttool

			if mk4exist then
				open path$+drillname$+suffix$+"1.mk4" for input as #1
				open path$+drillname$+suffix$+".mk4" for output as #2
				first=0
				do
					line input #1,s$
					if s$="$" and first=0 then 
						print #2,"X0.0Y0.0T0"
						first = 1
					end if
					if s$<>"" then print #2,s$
				loop until EOF(1) = 1
				close #1
				close #2
				kill path$+drillname$+suffix$+"1.mk4"
			end if

			if mk2exist then
				open path$+drillname$+suffix$+"1.mk2" for input as #1
				open path$+drillname$+suffix$+".mk2" for output as #2
				first=0
				do
					line input #1,s$
					if s$="$" and first=0 then 
						print #2,"X0.0Y0.0T0"
						first = 1
					end if
					if s$<>"" then print #2,s$
				loop until EOF(1) = 1
				close #1
				close #2
				kill path$+drillname$+suffix$+"1.mk2"
			end if
			
			if ex2exist then
				exelonfile1$ = path$+drillname$+suffix$+"1.ex2"
				exelonfile$ =  path$+drillname$+suffix$+".ex2"
				schfile1$ = path$+drillname$+suffix$+"1.sch"
				schfile$ = path$+drillname$+suffix$+".sch"
				if mpp <> -1 then
					if sz485x585 = 1 then
						STX# = 242.42
						STY# = 6.73
					else
						STX# = X%/2.0
						STY# = 66.0
						'wincall "copy "+exelonfile1$+" "+schfile1$
						open exelonfile1$ for input as #1
						open schfile1$ for output as #2
						do
							line input #1,s$
							print #2,s$
						loop until EOF(1) = 1
						close #1
						close #2
					end if
				else
					STX# = X%/2.0
					STY# = -6.25
				end if
				
				gosub editexelon
				open path$+drillname$+suffix$+".prl" for output as #2
					print #2,"T,O"
					print #2,USING "N1,A0,F##_,X:&.ex2"; Y%; drillname$+suffix$
				close #2
				if fileexists(schfile1$) then
					STY#=10 ' 28-01-2020 велели поставить 10, сменились станки, было 70
					if sz280x395 = 1 then
						STY#=10 ' 28-01-2020 велели поставить 10, сменились станки, было 66
					end if
					exelonfile1$ = schfile1$
					exelonfile$ = schfile$
					gosub editexelon
				end if
			end if
		end if
		edit_removelyr@ dopdrill
		
		' это если ДПП
		' удалить дополнительные отверстия'
		if mpp = -1 then
			setlayer@ drill
			layer_alloff@ 1
			edit_delete@
			if Z# < Y% then
				axy@ 10.0000,Z#
			end if
			if Z#+45 < Y% then
				axy@ 10.0000,Z#+45
			end if
			if Z#+45+90 < Y% then
				axy@ 10.0000,Z#+45+90
			end if
			if Z#+45+90+45 < Y% then
				axy@ 10.0000,Z#+45+90+45
			end if
			back@
		end if
	next
end if
' конец сверловок '

if mis>0 then
	for frezlay=1 to mis
		if mir(frezlay)=1 then
			if frezlay > 1 then
				dop$ = "-" + str$(frezlay) 
			end if
			drillnamef$ = drillname$ + dop$
			
			if frzexist then
				' выгнать'
				tablefile$ = tablepath$+"\tables4.lst"
				selected_layer = mi(frezlay)
				GOSUB corecttool
				'4. Настройка вывода mill mashine
				setup_fmtype@ 2,7
				'mill машина - SIEB & MEYER 3000
				setup_fmtunit@ 2,1
				'mill mashine - metric
				setup_fmtdigits@ 2,3,3
				'mill mashine - 3.3 digit
				setup_fmtmode@ 2,0
				'mill mashine - absolute
				setup_fmtzero@ 2,2
				'mill mashine - zero none
				if sz485x585 =1 then
					' 10-08-2012 Соковнин сказал что большие всегда на плите будут фрезероваться.'
					head$= path$ + "header4.hdr"
					' 14-06-2018 выяснилось что когда-то для 4 поменялся заголовок, а фрезеровке то нужно старый, там дописал
					head$= path$ + "headerf.hdr"
				else
					head$= path$ + "headerf.hdr"
				end if
				mill_sethdrfile@ head$
				'header file
				nc_exp_drill_Exc_toolingoffset@ 0
				nc_exp_mill_Exc_decimals@ 1
				nc_exp_mill_IncludeDrills@ 1
				Expmill_generate_Header@ 0
				nc_exp_milltool_infeed@ 1
				nc_exp_milltool_retract@ 1
				nc_exp_milltool_RPMs@ 1

				'5. Собственно вывод фрезеровки
				millPCB_out@ mi(frezlay),path$+drillnamef$+"1.frz"
				'	mill_out@ mill,path$+name$+"1.frz"
				Expmill_generate_Header@ 1

				'7. дополнение файла фрезеровки
				open path$+drillnamef$+"1.frz" for input as #1
				open path$+drillnamef$+".frz" for output as #2
				first=0

				do
					line input #1,s$
					if s$="$" and first=0 then 
						print #2,"X0.0Y0.0T0"
						first = 1
					end if

					if s$<>"" then print #2,s$
				loop until EOF(1) = 1
				close #1
				close #2
				
				kill path$+drillnamef$+"1.frz"
			end if

			' экселон фрезеровка
			minimafr = 1
			GOSUB corecttool
			minimafr = -1
			setup_fmtype@ 2,5
			setup_fmtunit@ 2,1
			setup_fmtdigits@ 2,3,3
			setup_fmtmode@ 2,0
			setup_fmtzero@ 2,2
			nc_exp_mill_IncludeDrills@ 1
			nc_exp_mill_Exc_decimals@ 1
			nc_exp_mill_Exc_toolingoffset@ 1
			
			
			nc_exp_drilltool_infeed@ 0
			nc_exp_drilltool_retract@ 0
			nc_exp_drilltool_RPMs@ 0
			nc_exp_drilltool_MaxHits@ 0
			nc_exp_milltool_infeed@ 0
			nc_exp_milltool_retract@ 0
			nc_exp_milltool_RPMs@ 0
			
			
			mill_out@ mi(frezlay),path$+drillnamef$+"1.fx2"

			'7.1 дополнение файла фрезеровки exelon
			if mpp <> -1 then
				STX# = X%/2.0
				STY# = 66.0
				if sz485x585 then
					Z# = 202.5
				else
					Z# = 107.5
				end if
			else
				STX# = 190
				STY# = 210-Z#
			end if
			
			STX# = 190
			STY# = 210-Z#

			exelonfile1$ = path$+drillnamef$+"1.fx2" 
			exelonfile$ = path$+drillnamef$+".fx2" 
			gosub editexelon
			
			if fileexists(path$+drillnamef$+"1.mx1") then
				frezy(1) = 0
				exelonfile1$ = path$+drillnamef$+"1.mx1" 
				exelonfile$ = path$+drillnamef$+".mx1" 
				gosub editexelon
			end if
		end if
	next
end if

'8. удаление лишних файлов
head$= path$ + "header4.hdr"
kill head$
head$= path$ + "header2.hdr"
kill head$
head$= path$ + "headerf.hdr"
kill head$

close_msg

if drillnew <> -1 then edit_removelyr@ drillnew
view_redraw@

' создать файл копирования сверловки

' записать данные в базу
dopurl$="mkrfrz"+"&customer="+customer$+"&board="+board$+"&drillname="+drillname$+"&mpp="+str$(mpp)+"&sizex="+str$(X%)+"&sizey="+str$(Y%)+"&path="+path$
outfile$=chr$(34)+path$+"cx.bat"+chr$(34)

!include "baseupdate.scr"


' закончить, дальше идут подпрограммы '
' закрыть текущий без сохранения'
if closefile% then 
	if Version! > 7 then 
		setquit@ 
	else 
	' в седьмом каме нет закрытия. Пришлось делать средствами системы. Закрывая с детьми taskkill /t убивал и бат файл, не давая удалить себя. Тасклист резал вывод по /v, пришлось вывод списком делать и фильстровать только имя файла.'
		open path$+"closecam.bat" for output as #1
			print #1,"chcp 1251"
			print #1,"for /f "+chr$(34)+"usebackq tokens=2"+chr$(34)+" %%a in (`tasklist ^| find "+chr$(34)+"camnt95"+chr$(34)+"`) do ("
			print #1,"tasklist /v /fi "+chr$(34)+"PID eq %%a"+chr$(34)+" /fo list | find "+chr$(34)+"pcb"+chr$(34)+" | find "+chr$(34)+"\"+shortfilename$+chr$(34)+" /c >nul && taskkill /PID %%a /F"
			print #1,")"
			print #1,"erase /f /q "+chr$(34)+path$+"closecam.bat"+chr$(34)
		close #1

		s1$ = chr$(34)+path$+"closecam.bat"+chr$(34)
		WinExec s1$,MinimizedNoFocus
	end if
end if
' тут конец, выйти (exit, break)
goto mkrfrz_endscript

' подпрограмма корекции параметров инструментов'
corecttool:
	lines%=0

	OPEN tablefile$ FOR INPUT as #1
		' тут бы не плохо условие linrs<200? а то выход за массив будет, но пусть лучше падает скрипт'
		DO WHILE NOT EOF (1) =1
			lines% = lines% + 1;
			INPUT #1, d#(lines%), if#(lines%), of#(lines%), r#(lines%)
			frezy(lines%)=0
		LOOP
	CLOSE #1

	' пройти только по нужному слою'
	setlayer@ selected_layer
	table = tooltable!
	number = 1;
	fre=1;
	for i=1 to Highesttoolref! 
		set_current_toolref@ i
		if Toolused! =1 then
			sizes#(number)=toolsize!
			numbers(number)=i
			number=number+1
		else
			nc_delete_tool@ table,i
		end if
	next
	number=number-1
	'sort'
	for j=1 to number-1
		for i=1 to number-j
			if sizes#(i)>sizes#(i+1) then
				temp#=sizes#(i)
				sizes#(i)=sizes#(i+1)
				sizes#(i+1) = temp#
				temp=numbers(i)
				numbers(i)=numbers(i+1)
				numbers(i+1)=temp
			end if
		next
	next
	' end sort'
	' renumber '
	for i=1 to number
		nc_set_tool_id@ table,numbers(i),i
	next

	'end renumber'
	for i=1 to number 
		toolref = numbers(i)
		set_current_toolref@ toolref
		
		if tooltype! = 2 then goto freza

		for index=1 to lines%
			if d#(index)=toolsize! then exit for
		next
		if index>lines% then goto notoolintable
		if units! = 1 then
			nc_set_tool_infeed@ table,toolref,if#(index)*10/6
			nc_set_tool_outfeed@ table,toolref,of#(index)*10/6
		else
			nc_set_tool_infeed@ table,toolref,if#(index)*10/6
			nc_set_tool_outfeed@ table,toolref,of#(index)*10/6
		end if
		nc_set_tool_rpms@ table,toolref,r#(index)*1000
		goto notoolintable
		
		freza:
		'print_msg "Есть фреза!" + CR!
		frezy(fre) = Toolnum! 'было i вместо Toolnum!'
		fre = fre + 1
		nc_set_tool_infeed@ table,toolref,8.3333
		nc_set_tool_outfeed@ table,toolref,75
		nc_set_tool_rpms@ table,toolref,30000
		ts# = toolsize!
		if ts# < 1 then
			ts# = 1 ' проверка на маленькие фрезы'
		end if
		nc_set_tool_feedrate@ table,toolref,ts#*10/6 'преобразование
		'25-05-2016 корекция фидрейта для Плюритек и Минима'
		if minimafr=1 then
			if ts#=1 then
				nc_set_tool_feedrate@ table,toolref,4 
			end if
			if ts#=2 then
				nc_set_tool_feedrate@ table,toolref,15 
			end if
			if ts#=3 then
				nc_set_tool_feedrate@ table,toolref,16 
			end if
		end if
		notoolintable:
	next

return

' подпрограмма создания заголовка для zieb und mayer'
createheader:
	open head$ for output as #1
		print #1,"%%3000"
		' двусторонние вычисление высоты первой кнопки для фрезеровки кратной 10'
		if mpp = -1 then
			zz%=((Y%-180)/2)/10
			Z#=zz%*10
			if Z#<=0 then Z#=10
			xprob# = 10
		else
			if sz485x585 = 1 then
			' 4-06-2018'
				Z#=202.5
				xprob# = 10
			end if
		end if

		print #1, USING "M47, ###*###  ##";X%;Y%;Z#

		' сверловки для многослоек'
		if fr <> 1 then
			if hh = 2 then
				ny#=-6.25
				nx# = -X%/2.0
				if sz230x395 = 1 then
					nx#=-115
					ny#=70
				end if
				if sz280x395 = 1 then
					nx#=-140
					ny#=70
				end if
				if sz280x380 = 1 then
					nx#=-140
					ny#=85
				end if
			end if
			if hh=4 then
				'07-04-2017 двусторонние будут делаться на штифтах'
				ny# = 100.75 ' это высилялось как y координата нижнего штифта минус 6.25
				nx# = 270-X%/2.0 ' это x координата штифта
				if sz230x395 = 1 then
					nx#=150
					ny#=203.0
				end if
				if sz280x395 = 1 then
					nx#=125
					ny#=203.0
				end if
				if sz485x585 = 1 then
					nx#=27.5
					ny#=115
				end if
			end if
		else
			'Фрезеровка'
			nullx# = 99.375
			nully# = 277.375
			nx# = nullx#-xprob#
			ny# = nully#-Z#
		end if

		print #1, USING "X##.###Y##.###M50";nx#;ny#
	close #1
return

' подпрограмма редактирования exelon'
editexelon:
	open exelonfile1$ for input as #1
	open exelonfile$ for output as #2

		first=0
		do
			line input #1,s$
			
			if left$(s$,1) = "T" then
				'for i=1 to 10 
				i=1
					fff:
					if frezy(i) = 0 then goto breakfr
					if left$(s$,4) = FMTUSING$("T0#C",frezy(i)) and frezy(i) < 10 then
						' MessageBox FMTUSING$("T0#C",frezy(i)) + str$(i)
						s$ = left$(s$,4) + "-" + MID$(s$,5)
						goto breakfr
					end if
					if left$(s$,4) = FMTUSING$("T##C",frezy(i)) and frezy(i) >= 10 then
						s$ = left$(s$,4) + "-" + MID$(s$,5)
						goto breakfr
					end if
					i = i + 1
				if i<=4 then goto fff
				'next
				nnn:
			end if
			breakfr:

			if left$(s$,3) = "M06" then
				'если попадается опциональная остановка, которая в файле стоит в нуле, да еще и сжатом, 
				' поменяем на просто отъезд подальшею В примере указаны координаты X325.0Y770.0
				' 14:28 не получилось :(
				print #2,"M06X0.0Y70.0"
			else
				' писать строку
				' почему f042 я уже не помню
				if s$<>"" and s$<>"F042" then print #2,s$
			end if	
			if s$="%" and first=0 then 
				if sz485x585 = 1 then
					print #2,USING "G93X-##.###Y#.###"; STX#; STY#
				else
					print #2,USING "G93X-##.###Y##.###"; STX#; STY#
				end if
				first = 1
			end if
		loop until EOF(1) = 1
	close #1
	close #2
	kill exelonfile1$
return

mkrfrz_endscript:
