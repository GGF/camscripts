util_camed@
setgridsnap@ 0
setsnap@ 0

dim d#(200)'diametr
dim if#(200)'in feed
dim of#(200)'out feed
dim r#(200)'rpms
dim numbers(200) ' использованые номера сверел
dim sizes#(200) ' использованые номера сверел
dim frezy(200)
dim drr(10)
dim mir(10)
fre = 1

tablepath$="x:\tool\camscripts"
' нужно хоть один файл обозначить
tablefile$ = tablepath$+"\tables4.lst"

X% = dbmaxx!
Y% = dbmaxy!
' 16-01-2015 когда добавились в двусторонних 5мм  с каждой стороны нужно правильно считать размер заготовки'
if dbminx!=-5 then
	X%=X%-5
	Y%=Y%-5
end if
if X%<Y% then vert%=1
tochnovert%=-1
drillnew = -1
dopdrill = -1
scalex# = 1
scaley# = 1

xprob# = 10 ' все! это пробивка для фрезеровки
yprob# = (Y%-180)/2.0
mk2exist = 0 ' 18-02-2020 больше нет ' есть двухшпиндельная сверловка
mk4exist = 0 ' 18-02-2020 больше нет ' есть четырехшпиндельная сверловка
ex2exist = 1 ' есть сверловка для экселона специально ниже перехода мимо формы чтобы правильно работало формирование s32 в надевании рамки// 07-04-2017 удрвл вывод в надевании
frzexist = 0 ' 18-02-2020 больше нет
mx1exist = 0 ' 18-02-2020 больше нет
fx2exist = 1 ' добавил чтоб однообразно, раньше всегда была

if drillname$ <> "" then goto noform

!INCLUDE "getpathandname.scr"
!include "layersset.scr"

' проверка на поворот, если в 0,0 есть флешь d10 значит точно не надо поворачивать, и если в 0,dbmaxx! есть то тоже точно не надо
if comp<>-1 then
	setlayer@ comp
	layer_alloff@ 1
	query_dcode@
	axy@ 0,0
	back@
	break
	if Activedcode!=10 then
		tochnovert%=10;
		vert%=0
	else
		query_dcode@
		axy@ dbmaxx!,0
		back@
		if Activedcode!=10 then
			tochnovert=10;
			vert%=1
		else
			tochnovert%=abs(X%-Y%)*100/X% ' процентное отношение сторон'
			
			' не известно точно ли меньше 10 процентов будем спрашивать'
			
		end if
	end if
end if

' определение имени
if fileexists(path$+"copper.rpt") then 
	open path$+"copper.rpt" for input as #1
		input #1,drillname$
	close #1
	if len(drillname$) > 8 then
		MACRO_PLAY "createcoppers.scr"
		drillname$ = name$
	end if
else 
	MACRO_PLAY "createcoppers.scr"
	drillname$ = name$
end if

' вычисление кэфициэнтов
if 1=1 then
	if mpp = -1 then
		s32name$ = "dpp.s32"
	else
		s32name$ = "mpp"+str$(layers)+".s32"
		if not fileexists(path$+s32name$) then
			s32name$ = "mpp.s32"
		end if
	end if

	dim xscal#(100)
	dim yscal#(100)
	xmin# = 1000
	Ymin# = 1000
	xmax# = 0
	ymax# = 0

	if fileexists(path$+s32name$) then 
		open path$+S32name$ for input as #1
			do UNTIL EOF (1)
			input #1,hhh$
			if left$(hhh$,1) = "A" then
				num# = val(MID$(hhh$,2))
				input #1,hhh$
				xscal#(num#) = val(MID$(hhh$,INSTR(1,hhh$," ")))
			end if
			if left$(hhh$,1) = "B" then
				num# = val(MID$(hhh$,2))
				input #1,hhh$
				yscal#(num#) = val(MID$(hhh$,INSTR(1,hhh$," ")))
			end if
			LOOP
		close #1
		for num%=2 to layers%+1
			if ymin# > yscal#(num%) AND yscal#(num%)>0 then
				ymin# = yscal#(num%)
			end if
			if ymax# < yscal#(num%) AND yscal#(num%)>0 then
				ymax# = yscal#(num%)
			end if
			if xmin# > xscal#(num%) AND xscal#(num%)>0 then
				xmin# = xscal#(num%)
			end if
			if xmax# < xscal#(num%) AND xscal#(num%)>0 then
				xmax# = xscal#(num%)
			end if
		next
		scalex# = (xmin#+xmax#)/2.0/xscal#(1)
		scaley# = (ymin#+ymax#)/2.0/yscal#(1)
	end if
end if
' Конец вычисления коэффициэнтов'

' самая новая пробивка
if mpp <> -1 then
	if X% = 280 and Y% = 395 then
		sz280x395 = 1
	end if

	if X% = 485 and Y% = 585 then
		sz485x585 = 1
		mk2exist = 0 ' нет двухшпиндельной сверловки
	end if

	if X% = 230 and Y% = 395 then
		sz230x395 = 1
	end if
end if

if scalex#>1.0005 or scalex# < 0.9995 or scaley#>1.0005 or scaley#<0.9995 then
	utf2chr$ = "Странные коэффициэнты сжвтия Х="
	GOSUB utf2chr
	msg$ = utf2chr$
	utf2chr$ = "Вы уверенны?"
	GOSUB utf2chr
	title$ = utf2chr$
	messagebox msg$+str$(scalex#)+", Y="+str$(scaley#),title$,OKCANCEL,MacroEnd%
	if MacroEnd% = 2 then end
end if

if scalex#>1.001 or scalex# < 0.998 or scaley#>1.001 or scaley#<0.998 then
	utf2chr$ = "Я не буду это сжимать, поставлю единицу"
	GOSUB utf2chr
	msg$ = utf2chr$
	utf2chr$ = "Оооочень странные коэффициэнты"
	GOSUB utf2chr
	title$ = utf2chr$
	messagebox msg$,title$,OKCANCEL,MacroEnd%
	if MacroEnd% = 2 then end
	scalex#=1
	scaley#=1
end if

frr = 0
closefile% = 1

DPP:

utf2chr$ = "Сверловка и фрезеровка"
GOSUB utf2chr
title$ = utf2chr$

OpenForm #1,70,7,title$+" (c) Sch'arapoff", character, okcancel
if mpp <> -1 then
	frr=0
	vert% = 1
else
	' На кондукторах нужно выбирать
	if (comp% = -1 AND solder% = -1) then
		tochnovert% = 0
	end if
	if tochnovert%<9 then
		utf2chr$ = "Вертикально?"
		GOSUB utf2chr
		msg$ = utf2chr$	
		AddForm #1, 0, 0, msg$,check,vert%
	end if
end if

utf2chr$ = "Закрыть по окончании?"
GOSUB utf2chr
msg$ = utf2chr$
AddForm #1, 40, 0, msg$,check,closefile%

utf2chr$ = "Сверловки:"
GOSUB utf2chr
msg$ = utf2chr$
AddForm #1, 0, 1, msg$,print
for i = 1 to drs
	shiftx = 13+(i-1)*(55/drs)
	drr(i) = 1
	AddForm #1, shiftx, 1, dr$(i),check,drr(i)
next

utf2chr$ = "Фрезеровки:"
GOSUB utf2chr
msg$ = utf2chr$
AddForm #1, 0, 2, msg$,print
for i = 1 to mis
	shiftx = 20+(i-1)*20
	mir(i) = 1
	AddForm #1, shiftx, 2, mi$(i),check,mir(i)
next

utf2chr$ = "Имя файла:"
GOSUB utf2chr
msg$ = utf2chr$
AddForm #1, 0, 3, msg$,input,drillname$
utf2chr$ = "Сжатие по"
GOSUB utf2chr
msg$ = utf2chr$
AddForm #1,0, 5, msg$+" X", input,scalex#
AddForm #1,35, 5, msg$+" Y", input,scaley#
DisplayForm #1
Canceled% = formcancel(1)
DeleteForm #1

if Canceled% = 1 then goto mkrfrz_endscript

noform:
utf2chr$ = "Идет обработка... Ждите!"
GOSUB utf2chr
msg$ = utf2chr$
print_msg msg$

' развернуть если вертикальное
if vert% <> 1 then
	'view_all@
	layer_alloff@ 0
	edit_rotate@
	setsnap@ 0
	edit_selectall2@ 
	axy@ 0,Y%
	back@
	end@
	edit_move@
	edit_selectall2@ 
	axy@ 0,Y%
	axy@ 0.0000,0.0000
	back@
	temp = Y%
	Y% = X%
	X% = temp
	vert% = 1
	
	' поменять сжаттие местами'
	temp# = scalex#
	scalex# = scaley#
	scaley# = temp#
end if
' --- конец развернуть --- '


'1. создание файлов заголовка, должны быть до сверления под фрезеровку потому что вычисляется Z
if 1=1 then
	if mpp <> -1 then
		Z# = yprob#
	end if 

	fr = 1
	head$= path$ + "headerf.hdr"
	gosub createheader 
	fr=0

	hh = 4; ' 4 шпинделя
	head$= path$ + "header4.hdr"
	gosub createheader 

	head$= path$ + "header2.hdr"
	hh = 2; ' 2 шпинделя
	gosub createheader 
end if
' -- конец файлов заголовка -- '

if drs > 0 then
	for drlay = 1 to drs
		if drr(drlay) = 0 then
			drill = -1
		else
			drill = dr(drlay)
		end if
		if drlay <> 1 then
			suffix$="-"+str$(drlay)
		end if
		if drill <> -1 then
			if scalex# <> 1 OR scaley# <> 1 then 
				drillnew = Blanklayer!
				setlayer@ drill
				layer_alloff@ 1
				' сместить
				edit_move@
				edit_selectall2@
				axy@ 0,0
				axy@ -X%/2,-Y%/2
				back@
				' сжать
				edit_scale@ drill,drillnew,scalex#*100000000,scaley#*100000000
				' layer_alloff@ 0
				drill = drillnew
				' сместить на место
				edit_move@
				edit_selectall2@
				axy@ -X%/2,-Y%/2
				axy@ 0,0
				back@
			end if

			if mk2exist then
				open path$+drillname$+".mk2.old" for APPEND as #1
					print #1,"Этот файлы были сжаты САМ средствами. Обязательно вывести новый!!!"
					print #1,Time!+"   "+str$(scalex#)+"   "+str$(scaley#)
				close #1
			end if
			if mk4exist then
				open path$+drillname$+".mk4.old" for APPEND  as #1
					print #1,"Этот файлы были сжаты САМ средствами. Обязательно вывести новый!!!"
					print #1,Time!+"   "+str$(scalex#)+"   "+str$(scaley#)
				close #1
			end if
			if ex2exist then
				open path$+drillname$+".ex2.old" for APPEND  as #1
					print #1,"Этот файлы были сжаты САМ средствами. Обязательно вывести новый!!!"
					print #1,Time!+"   "+str$(scalex#)+"   "+str$(scaley#)
				close #1
			end if
		end if ' drill <> -1

		' Добавить сверловку под фрезеровку если двусторонняя
		' а вот если многослойная добавлять под фрезеровку отвертия, так как они не должны сжиматься'
		' вот вопрос как убрать со старых файлов?'
		
		' для начала создадим доп слой в любом случае'
		if 1=1 then
			'Highest tool table number
			HighestNCT%=-1
			setlayer@ 1
			for lay=1 to Highestlayer!
				setlayer@ lay
				if Layertype! = 21 and Tooltable! > HighestNCT% then 
				HighestNCT%=Tooltable!
				end if
			next
			for tab = HighestNCT%+1 to 100
				'set_nc_table@ tab
				'if dbq_tooltable! = tab then
					nc_delete_table@ tab
				'end if
			next
			dopdrill = Blanklayer!
			doptable = HighestNCT%+1
			nc_delete_table@ doptable
			nc_add_table@ doptable
			nc_set_table_type@ doptable,3
			nc_set_table_name@ doptable,"Dop Drill Tool"
			nc_add_tool@ doptable,1
			nc_set_tool_id@ doptable,1,1
			nc_set_tool_type@ doptable,1,3
			nc_set_tool_size@ doptable,1,3.0000
			'util_nced@
			setsnap@ 0
			edit_layer@ dopdrill,-2,-2,21,0
			nc_assign_tool_table_to_layer@ dopdrill,doptable
			nc_set_layer_rank@ dopdrill,1
		end if
		' создали доп слой
		
		' это если многослойка
		' удалить отверстия, ведь они уже сжаты'
		if mpp <> -1 then
			setlayer@ drill
			layer_alloff@ 1
			edit_delete@
			edit_group@
			axy@ 10.0000-5.0,Z#-5.0
			axy@ 10.0000+5.0,Z#+45+90+45+5.0
			back@
			delete_edits@ 4,1
			back@
		end if

		setlayer@ drill
		selected_layer = drill

		if drill <> -1 and mx1exist then
			' минима
			'07-04-2017 это минима, на ней не сверлятся отверстия для фрезеровки потому что на ней и фрезеруется
			' вывод 
			expdrl_generate_Header@ 1
			export_XYmodal@ 1,0
			nc_exp_drill_t00@ 0
			expdrl_generate_Tools@ 1
			setup_fmtype@ 1,5
			setup_fmtunit@ 1,1
			setup_fmtdigits@ 1,3,3
			setup_fmtmode@ 1,0
			nc_exp_drilltool_infeed@ 0
			nc_exp_drilltool_retract@ 0
			nc_exp_drilltool_RPMs@ 0
			nc_exp_drilltool_MaxHits@ 0
			nc_exp_drill_Exc_decimals@ 1
			nc_exp_drill_Exc_toolingoffset@ 1
			''
			setup_fmtype@ 2,5
			setup_fmtunit@ 2,1
			setup_fmtdigits@ 2,3,3
			setup_fmtmode@ 2,0
			setup_fmtzero@ 2,2
			nc_exp_mill_IncludeDrills@ 1
			nc_exp_mill_Exc_decimals@ 1
			nc_exp_mill_Exc_toolingoffset@ 1
			nc_exp_milltool_infeed@ 0
			nc_exp_milltool_retract@ 0
			nc_exp_milltool_RPMs@ 0
			tablefile$ = tablepath$+"\tables4.lst"
			minimafr = 1
			GOSUB corecttool
			minimafr = -1
			drext$="mx1"
			mill_out@ drill,path$+drillname$+suffix$+"1."+drext$ ' для плат в металлизированной фрезеровкой'
		end if ' drill <> -1 and mx1exist

		' теперь если это excelon или многослойка поставим отверстия в 10 мм от края для фрезеровки'
		'7-04-2017 теперь добавляем для всех видов сверловки потому, что все сверлятся на двух штифтах'
		setlayer@ dopdrill
		layer_alloff@ 1
		nc_add_drill_hit@
		setsnap@ 0
		if Z# < Y% then
			axy@ 10.0000,Z#
		end if
		if Z#+45 < Y% then
			axy@ 10.0000,Z#+45
		end if
		if Z#+45+90 < Y% then
			axy@ 10.0000,Z#+45+90
		end if
		if Z#+45+90+45 < Y% then
			axy@ 10.0000,Z#+45+90+45
		end if
		back@
		edit_move@
		edit_selectall2@ 
		movetolayer@ drill
		back@
		' конец сверления отв под фрезеровку '


		if drill <> -1 and mk4exist then
			tablefile$ = tablepath$+"\tables4.lst"
			selected_layer = drill
			GOSUB corecttool

			' чтобы влючить фрезеровку поставил сюда текст из фрезеровки

			'4. Настройка вывода mill mashine
			setup_fmtype@ 2,7
			'mill машина - SIEB & MEYER 3000
			setup_fmtunit@ 2,1
			'mill mashine - metric
			setup_fmtdigits@ 2,3,3
			'mill mashine - 3.3 digit
			setup_fmtmode@ 2,0
			'mill mashine - absolute
			setup_fmtzero@ 2,2
			'mill mashine - zero none
			head$= path$ + "header4.hdr"
			mill_sethdrfile@ head$
			'header file
			nc_exp_drill_Exc_toolingoffset@ 0
			nc_exp_mill_Exc_decimals@ 1
			nc_exp_mill_IncludeDrills@ 1
			Expmill_generate_Header@ 0
			nc_exp_drilltool_infeed@ 1
			nc_exp_drilltool_retract@ 1
			nc_exp_drilltool_RPMs@ 1
			nc_exp_drilltool_MaxHits@ 1
			nc_exp_milltool_infeed@ 1
			nc_exp_milltool_retract@ 1
			nc_exp_milltool_RPMs@ 1


			'5. Собственно вывод фрезеровки
			millPCB_out@ drill,path$+drillname$+suffix$+"1.mk4"
			'	mill_out@ mill,path$+name$+"1.frz"
			Expmill_generate_Header@ 1

		end if ' drill <> -1 and mk4exist

		if drill <> -1 and mk2exist then
			tablefile$ = tablepath$+"\tables2.lst"
			selected_layer = drill
			GOSUB corecttool

			'2. Настройка вывода drill mashine

			setup_fmtype@ 1,7
			'дрилл машина - SIEB & MEYER 3000
			setup_fmtunit@ 1,1
			'drill mashine - metric
			setup_fmtdigits@ 1,3,3
			'drill mashine - 3.3 digit
			setup_fmtmode@ 1,0
			'drill mashine - absolute
			setup_fmtzero@ 1,2
			'drill mashine - zero none
			head$= path$ + "header2.hdr"
			drill_sethdrfile@ head$
			'header file
			nc_exp_drill_Exc_toolingoffset@ 0
			nc_exp_drill_Exc_decimals@ 1
			Expdrl_generate_Header@ 0
			nc_exp_drilltool_infeed@ 1
			nc_exp_drilltool_retract@ 1
			nc_exp_drilltool_RPMs@ 1
			nc_exp_drilltool_MaxHits@ 1
			nc_exp_milltool_infeed@ 1
			nc_exp_milltool_retract@ 1
			nc_exp_milltool_RPMs@ 1


			'3. Собственно вывод сверловки
			drillPCB_out@ drill,path$+drillname$+suffix$+"1.mk2"
			'	drill_out@ drill,path$+name$+"1.mk2"
			Expdrl_generate_Header@ 1
		end if ' drill <> -1 and mk2exist

		' экселон
		if drill <> -1 and ex2exist then
			minimafr = 1
			GOSUB corecttool
			minimafr = -1

			drext$="ex2"
			''
			setlayer@ drill
			' вывод 
			expdrl_generate_Header@ 1
			export_XYmodal@ 1,0
			nc_exp_drill_t00@ 0
			expdrl_generate_Tools@ 1
			setup_fmtype@ 1,5
			setup_fmtunit@ 1,1
			setup_fmtdigits@ 1,3,3
			setup_fmtmode@ 1,0
			nc_exp_drilltool_infeed@ 0
			nc_exp_drilltool_retract@ 0
			nc_exp_drilltool_RPMs@ 0
			nc_exp_drilltool_MaxHits@ 0
			nc_exp_drill_Exc_decimals@ 1
			nc_exp_drill_Exc_toolingoffset@ 1
			''
			setup_fmtype@ 2,5
			setup_fmtunit@ 2,1
			setup_fmtdigits@ 2,3,3
			setup_fmtmode@ 2,0
			setup_fmtzero@ 2,2
			nc_exp_mill_IncludeDrills@ 1
			nc_exp_mill_Exc_decimals@ 1
			nc_exp_mill_Exc_toolingoffset@ 1
			nc_exp_milltool_infeed@ 0
			nc_exp_milltool_retract@ 0
			nc_exp_milltool_RPMs@ 0
			''
			'drillPCB_out@ drill,path$+drillname$+"1.ex2"
			mill_out@ drill,path$+drillname$+suffix$+"1."+drext$ ' для плат в металлизированной фрезеровкой'
		end if ' drill <> -1 and ex2exist

		'6. дополнение файла сверловки
		close_msg
		print_msg "Correct files." + CR! + "It is so long." + CR! + "Wait!!!"

		if drill <> -1 then
			'Вернем коректировку к оригинальной'
			tablefile$ = tablepath$+"\tables4.lst"
			selected_layer = drill
			GOSUB corecttool

			if mk4exist then
				open path$+drillname$+suffix$+"1.mk4" for input as #1
				open path$+drillname$+suffix$+".mk4" for output as #2
				first=0
				do
					line input #1,s$
					if s$="$" and first=0 then 
						print #2,"X0.0Y0.0T0"
						first = 1
					end if
					if s$<>"" then print #2,s$
				loop until EOF(1) = 1
				close #1
				close #2
				kill path$+drillname$+suffix$+"1.mk4"
			end if

			if mk2exist then
				open path$+drillname$+suffix$+"1.mk2" for input as #1
				open path$+drillname$+suffix$+".mk2" for output as #2
				first=0
				do
					line input #1,s$
					if s$="$" and first=0 then 
						print #2,"X0.0Y0.0T0"
						first = 1
					end if
					if s$<>"" then print #2,s$
				loop until EOF(1) = 1
				close #1
				close #2
				kill path$+drillname$+suffix$+"1.mk2"
			end if
			
			if ex2exist then
				exelonfile1$ = path$+drillname$+suffix$+"1.ex2"
				exelonfile$ =  path$+drillname$+suffix$+".ex2"
				schfile1$ = path$+drillname$+suffix$+"1.sch"
				schfile$ = path$+drillname$+suffix$+".sch"
				if mpp <> -1 then
					if sz485x585 = 1 then
						STX# = 242.42
						STY# = 6.73
					else
						STX# = X%/2.0
						STY# = 66.0
						'wincall "copy "+exelonfile1$+" "+schfile1$
					end if
					open exelonfile1$ for input as #1
					open schfile1$ for output as #2
					do
						line input #1,s$
						print #2,s$
					loop until EOF(1) = 1
					close #1
					close #2
				else
					STX# = X%/2.0
					STY# = -6.25
				end if
				
				gosub editexelon
				open path$+drillname$+suffix$+".prl" for output as #2
					print #2,"T,O"
					print #2,USING "N1,A0,F##_,X:&.ex2"; Y%; drillname$+suffix$
				close #2
				if fileexists(schfile1$) then
					STY#=10 ' 28-01-2020 велели поставить 10, сменились станки, было 70
					if sz280x395 = 1 then
						STY#=10 ' 28-01-2020 велели поставить 10, сменились станки, было 66
					end if
					exelonfile1$ = schfile1$
					exelonfile$ = schfile$
					gosub editexelon
				end if
			end if
		end if ' drill <> -1
		edit_removelyr@ dopdrill
		
		' это если ДПП
		' удалить дополнительные отверстия'
		if mpp = -1 then
			setlayer@ drill
			layer_alloff@ 1
			edit_delete@
			if Z# < Y% then
				axy@ 10.0000,Z#
			end if
			if Z#+45 < Y% then
				axy@ 10.0000,Z#+45
			end if
			if Z#+45+90 < Y% then
				axy@ 10.0000,Z#+45+90
			end if
			if Z#+45+90+45 < Y% then
				axy@ 10.0000,Z#+45+90+45
			end if
			back@
		end if ' mpp=-1
	next ' drlay
end if ' drills bigger zero
' конец сверловок '

if mis>0 then
	for frezlay=1 to mis
		if mir(frezlay)=1 then
			if frezlay > 1 then
				dop$ = "-" + str$(frezlay) 
			end if
			drillnamef$ = drillname$ + dop$

			selected_layer = mi(frezlay)

			if frzexist=1 then
				' слоый выбран выше, вызов здесь чтобы не вызывать лишний раз если не нужна frz
				GOSUB corecttool			
				' выгнать'
				tablefile$ = tablepath$+"\tables4.lst"
				'4. Настройка вывода mill mashine
				setup_fmtype@ 2,7
				'mill машина - SIEB & MEYER 3000
				setup_fmtunit@ 2,1
				'mill mashine - metric
				setup_fmtdigits@ 2,3,3
				'mill mashine - 3.3 digit
				setup_fmtmode@ 2,0
				'mill mashine - absolute
				setup_fmtzero@ 2,2
				'mill mashine - zero none
				if sz485x585 =1 then
					' 10-08-2012 Соковнин сказал что большие всегда на плите будут фрезероваться.'
					head$= path$ + "header4.hdr"
					' 14-06-2018 выяснилось что когда-то для 4 поменялся заголовок, а фрезеровке то нужно старый, там дописал
					head$= path$ + "headerf.hdr"
				else
					head$= path$ + "headerf.hdr"
				end if
				mill_sethdrfile@ head$
				'header file
				nc_exp_drill_Exc_toolingoffset@ 0
				nc_exp_mill_Exc_decimals@ 1
				nc_exp_mill_IncludeDrills@ 1
				Expmill_generate_Header@ 0
				nc_exp_milltool_infeed@ 1
				nc_exp_milltool_retract@ 1
				nc_exp_milltool_RPMs@ 1

				'5. Собственно вывод фрезеровки
				millPCB_out@ mi(frezlay),path$+drillnamef$+"1.frz"
				'	mill_out@ mill,path$+name$+"1.frz"
				Expmill_generate_Header@ 1

				'7. дополнение файла фрезеровки
				open path$+drillnamef$+"1.frz" for input as #1
				open path$+drillnamef$+".frz" for output as #2
				first=0

				do
					line input #1,s$
					if s$="$" and first=0 then 
						print #2,"X0.0Y0.0T0"
						first = 1
					end if

					if s$<>"" then print #2,s$
				loop until EOF(1) = 1
				close #1
				close #2
				
				kill path$+drillnamef$+"1.frz"
			end if

			if fx2exist=1 then
				' экселон фрезеровка
				minimafr = 1
				GOSUB corecttool ' если есть только фрезеровка падает с неправильным количеством for-next, не был задан selected_layer
				minimafr = -1
				setup_fmtype@ 2,5
				setup_fmtunit@ 2,1
				setup_fmtdigits@ 2,3,3
				setup_fmtmode@ 2,0
				setup_fmtzero@ 2,2
				nc_exp_mill_IncludeDrills@ 1
				nc_exp_mill_Exc_decimals@ 1
				nc_exp_mill_Exc_toolingoffset@ 1
				
				' почему то всё выключили для экселона, сверловщикам приходится вводить вручную скорость входа фрезы
				' drill
				nc_exp_drilltool_infeed@ 0
				nc_exp_drilltool_retract@ 0
				nc_exp_drilltool_RPMs@ 0
				nc_exp_drilltool_MaxHits@ 0
				' mill 
				nc_exp_milltool_infeed@ 1 ' добавил чтоб не вводили вручную.
				nc_exp_milltool_retract@ 0
				nc_exp_milltool_RPMs@ 0
				
				
				mill_out@ mi(frezlay),path$+drillnamef$+"1.fx2"

				'7.1 дополнение файла фрезеровки exelon
				if mpp <> -1 then
					STX# = X%/2.0
					STY# = 66.0
					if sz485x585 then
						Z# = 202.5
					else
						Z# = 107.5
					end if
				else
					STX# = 190
					STY# = 210-Z#
				end if
				
				STX# = 190
				STY# = 210-Z#

				exelonfile1$ = path$+drillnamef$+"1.fx2" 
				exelonfile$ = path$+drillnamef$+".fx2" 
				gosub editexelon
				
				if fileexists(path$+drillnamef$+"1.mx1") then
					frezy(1) = 0
					exelonfile1$ = path$+drillnamef$+"1.mx1" 
					exelonfile$ = path$+drillnamef$+".mx1" 
					gosub editexelon
				end if ' file exist
			end if ' fx2exist
		end if ' frezlay
	next ' frezlay
end if ' mis>0
' конец фрезеровок

'8. удаление лишних файлов
if 1=1 then
	head$= path$ + "header4.hdr"
	kill head$
	head$= path$ + "header2.hdr"
	kill head$
	head$= path$ + "headerf.hdr"
	kill head$
end if ' удаление лишних файлов

close_msg ' "I am working... wait!!!"

if drillnew <> -1 then edit_removelyr@ drillnew
view_redraw@

' создать файл копирования сверловки
' записать данные в базу
dopurl$="mkrfrz"+"&customer="+customer$+"&board="+board$+"&drillname="+drillname$+"&mpp="+str$(mpp)+"&sizex="+str$(X%)+"&sizey="+str$(Y%)+"&path="+path$
outfile$=chr$(34)+path$+"cx.bat"+chr$(34)
!include "baseupdate.inc"

' закрыть проект
!include "closecam.inc"
' закончить, дальше идут подпрограммы '
goto mkrfrz_endscript

!include "correcttool.inc"

!include "snmheader.inc"

!include "editexelon.inc"

!include "utf2chr1251.inc"

mkrfrz_endscript:
